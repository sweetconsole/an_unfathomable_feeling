
label unfathomable_feeling_epilogue:
    $ new_chapter(0, u"Непостижимое чувство.\nПролог")
    $ persistent.sprite_time = "day"
    $ prolog_time()

    scene int_house_male_day

    play ambience ambience_int_cabin_day loop fadein 4

    $ renpy.pause(1.0, hard=True)

    "В последний день смены всё иначе. Все обычно носятся по лагерю, собирая вещи, одолженные знакомым. Упрашивают вожатую о чем-либо, или откровенно бесятся, стирают последствия \"королевской ночи\" и прощаются с друзьями. {w}Но не Семён."

    "Запершись в своём домике, он сидел на койке и торопливо писал что-то на листе бумаги в клетку. Парень был полностью сосредоточен на этом."
    
    show letter_miku with fade

    menu:
        "Оставить письмо в виде картинки или открыть текст?"

        "Оставить":
            jump unfathomable_feeling_epilogue_letter_image
        
        "Открыть текст":
            jump unfathomable_feeling_epilogue_letter_text


label unfathomable_feeling_epilogue_letter_image:
    window hide
    $ renpy.pause(999.0, hard=False)
    window show

    jump unfathomable_feeling_epilogue_2


label unfathomable_feeling_epilogue_letter_text:
    window hide
    $ renpy.pause(1.5, hard=True)
    $ set_mode_nvl()
    nvl show dissolve

    """ 
    Дорогая Мику
    Я знаю, что это письмо тебе не дойдёт, да я и не собирался его высылать, мне просто нужно выговориться, хотя бы этому листку. Ты не представляешь, каких трудов мне стоило достать этот лист! Ну ладно, это не важно. Важно то, что я не забывал тебя ни на минуту своего существования, хотя всё случилось так, как и сказал тот пионер. В лагере больше нет тебя. Вообще. Остался лишь пустующий музыкальный кружок.\n
    Иногда я захожу внутрь, вдыхаю этот аромат беззаботности и улетаю далеко-далеко, на несколько десятков смен назад. Когда мы были настоящими. Ты помнишь это, Мику? Ты помнишь наш первый поцелуй, поездку на остров? Конечно ты помнишь. Ты бы не простила себе забыть этого. Так вот, я вспоминаю всё это, и на душе становится тепло. Я беру гитару и начинаю наигрывать на ней ту самую мелодию, которой ты меня учила. У меня плохо получается, но я стараюсь, и буду стараться. Чтобы у меня было ещё одно воспоминание о тебе.\n
    Из ребят я стал общаться с Алисой и Ульяной, они дружные, и тоже любят музыку. Мы даже пару раз концерт устраивали. Столько ребят хлопало нам по ту сторону эстрады, но я отчётливо понимал, что кого-то там не хватает. Что не просто так пустует место рядом с Леной.\n
    Пионера я тоже видел. Он сказал, наверно, лучшее что я мог слышать в своей жизни. Пионер сказал, что ты выбралась отсюда. Что каким-то странным способом ты ушла из той смены в реальный мир, и все твои циклы захлопываются. Мы с ним много болтаем, рассуждаем о циклах, о том как влияет поведение на результат. Я отучил его от привычки всё время говорить, что выхода нет, ведь он есть, и ты прошла через него.
    Я бы написал тебе ещё много чего, я так хочу поговорить с тобой. Но время идёт, и мне пора. Мы договорились с пионером что он сотрет мне память.\n
    Все мои воспоминания о тебе.\n
    Я отдам ему этот листок, и каждая смена для меня будет как первый раз. Извини, но я должен. Потому что я понимаю, что скучая по тебе потихоньку схожу с ума. Прости, но по-другому нельзя. 
    """

    nvl hide dissolve
    $ renpy.pause(1.5, hard=True)
    $ set_mode_adv()
    window show

    jump unfathomable_feeling_epilogue_2


label unfathomable_feeling_epilogue_2:
    play ambience ambience_camp_center_rain loop fadein 4

    "Сложив тетрадный лист вдвое, он нарисовал какой-то символ. Взяв листок в руки Семён направился в сторону пристани, где была назначена встреча."
    
    scene ext_houses_rain with dissolve

    "Снаружи многое изменилось: место яркого солнца заняли тучи, из которых начал покрапывать дождь. Спустя несколько мгновений дождик превратился в настоящий ливень с порывами ледяного ветра, пробивающего насквозь." 
    
    scene ext_warehouse_rain with dissolve
    
    "Семён устало плёлся по тропинке, ведущей к пристани, он был расслаблен, и в его глазах что-то сверкало. Наверное, капли дождя, попавшие на лицо. Очередное дуновение ветра вырвало листок из рук юноши и отправило его в долгий полёт." 
    
    scene ext_boathouse_rain with dissolve
    
    "Герой лишь устало протянул руку и пошёл дальше, уже решительным шагом. Когда листок улетел, Семён понял, что нужно иногда отпускать старое. Эта мысль неприятно кольнула его сердце, но он уже не обращал внимания."

    show blink
    window hide
    $ renpy.pause(1.5, hard=True)

    scene int_kitchen
    show mi normal apron

    play ambience ambience_lake_shore_rainfall loop fadein 4

    hide blink
    show unblink
    $ renpy.pause(1.5, hard=True)
    window show

    play sound sfx_cooking_eggs_1

    "Мику, как обычно, готовила что-то на кухне. На весь дом разносился приятный аромат. За окном шёл обычный весенний дождь, стуча своими каплями по стеклу. Из-за чёрных туч изредка выглядывало солнце."
    
    play sound sfx_cooking_eggs_2

    "Мику спокойно перемешивала яйца в сковороде, не обращая внимание на окружение."

    play sfx sfx_blow_to_the_glass

    "Внезапно в окно что-то ударилось. Мику аккуратно отложила лопатку и подошла к нему. Тетрадный листок прижимался к стеклу порывами ветра, на нём красивым, чуть ли не каллиграфическим почерком было написано несколько слов: \"К...ж..и, би...т..ка, ме...п..к.\", и добавлено, уже другим почерком: \"Музыкальный кружок\"."
    "Рядом с последней надписью красовалось сердечко, нарисованное ручкой. Листок полностью промок, и было видно, что на обратной стороне что-то написано."
    
    "Внезапно ветер успокоился, и лист начал медленно отлипать от стекла. Девушка уже хотела открыть окно, чтобы взять его, но ещё один порыв ветра полностью отлепил его и унёс за собой ввысь."
    "Мику, немного постояв на месте, вернулась к плите. Она пересыпала содержимое сковородки на две тарелки и пошла в комнату."

    scene int_flat_night 
    show mi normal apron at left
    show sm normal shirt at right
    with dissolve

    mi "Держи."
    "Она поставила тарелку перед Семёном."
    
    show sm smile shirt with dspr

    me "Спасибо."
    "Парень улыбнулся."
    me "Как всегда, очень вкусно."
    
    mi "Но ты даже не попробовал!"
    
    show sm grin shir with dspr

    me "Я и так это знаю. Но ладно, только ради тебя."
    "Юноша усмехнулся, после чего принялся за завтрак."
    
    scene int_flat_day
    show mi normal apron at left
    show sm normal shirt at right
    with fade

    "Мику долго не отрывала взгляд от окна, ища что-то в нём. Погода улучшилась, и вместо дождя стояло прекрасное солнечное утро."
    
    me "Всё нормально?"
    "Семён вывел её из раздумий."
    
    show mi smile apron with dspr

    mi "Да, вполне."
    "Девушка улыбнулась ему, нехотя отвела взгляд от окна и начала есть."

    show blink
    window hide
    $ renpy.pause(1.5, hard=True)

    stop ambience

    scene anim prolog_1

    hide blink
    show unblink
    $ renpy.pause(1.5, hard=True)
    window show

    "У каждой истории есть свои выборы. Они бывают незначительные, решающие, тяжёлые, ложные и с несколькими вариантами развития событий. Иногда кажется, что от твоего выбора здесь и сейчас ничего не зависит."
    "У каждой истории есть свои выборы. И не существует историй, в которых от одного выбора зависела бы вся остальная судьба человека. Почти не существует..."